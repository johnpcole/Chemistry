      SUBROUTINE MAKE_ROT(BIG_J,ROT_J,ROT_K,E_LIMIT,ROTSTATE,STATEMAX,
     &                             ROT_I,ORBSTATE,DIASTATE,DEGSTATE)

C-    THIS SUBROUTINE CREATES THE ROTATIONAL DATA TABLE

      IMPLICIT NONE

      INTEGER B_MAX       ! COMBINATION QUANTUM NUMBER UPPER LIMIT
      INTEGER B_MIN       ! COMBINATION QUANTUM NUMBER LOWER LIMIT
      INTEGER B_VAL       ! COMBINATION QUANTUM NUMBER
      INTEGER BIG_J       ! TOTAL ANGULAR MOMENTUM QUANTUM NUMBER
      INTEGER DEGEN       ! ROTATIONAL DEGENERACY FACTOR
      REAL*8 E_LIMIT      ! AMOUNT OF AVAILABLE ENERGY
      INTEGER INDEX       ! ROTATIONAL STATES TABLE INDEX
      REAL*8 I_ENE        ! LINEAR ROTATIONAL ENERGY
      INTEGER I_MAX       ! LINEAR ROTATIONAL QUANTUM NUMBER LIMIT
      INTEGER I_VAL       ! LINEAR ROTATIONAL QUANTUM NUMBER
      REAL*8 J_ENE        ! ROTATIONAL ENERGY
      INTEGER J_MAX       ! ROTATIONAL QUANTUM NUMBER LIMIT
      INTEGER J_VAL       ! ROTATIONAL QUANTUM NUMBER
      REAL*8 K_ENE        ! ROTATIONAL ENERGY
      INTEGER K_MAX       ! ROTATIONAL QUANTUM NUMBER LIMIT
      INTEGER K_VAL       ! ROTATIONAL QUANTUM NUMBER
      INTEGER L_MAX       ! ORBITAL QUANTUM NUMBER UPPER LIMIT
      INTEGER L_MIN       ! ORBITAL QUANTUM NUMBER LOWER LIMIT
      INTEGER L_VAL       ! ORBITAL QUANTUM NUMBER
      INTEGER M_VAL       ! DEGENERACY COUNTING QUANTUM NUMBER
      INTEGER N_VAL       ! DEGENERACY COUNTING QUANTUM NUMBER
      INTEGER P,T         ! DATA TABLE FLAGS
      INTEGER R,D         ! DATA TABLE FLAGS
      REAL*8 R_ENE        ! PRODUCT ENERGY (EXCLUDES L_ENE)
      REAL*8 ROT_I        ! ROTATIONAL CONSTANT OF LINEAR FRAGMENT
      REAL*8 ROT_J        ! ROTATIONAL CONSTANT
      REAL*8 ROT_K        ! ROTATIONAL CONSTANT
      REAL*8 ROTSTATE     ! ROTATIONAL STATES TABLE
      INTEGER STATEMAX    ! ROTATIONAL STATES TABLE SIZE
      INTEGER ORBSTATE
      INTEGER DIASTATE
      INTEGER TERM_1
      INTEGER TERM_2
      INTEGER DEGSTATE

      DIMENSION ROTSTATE(0:STATEMAX)     ! ROTATIONAL STATES TABLE
      DIMENSION ORBSTATE(0:STATEMAX)          ! L_VAL TABLE
      DIMENSION DIASTATE(0:STATEMAX)          ! I_VAL TABLE
      DIMENSION DEGSTATE(0:STATEMAX)

      PARAMETER (R=-1,D=-2)                   ! DATA TABLE FLAGS
      PARAMETER (P=-1,T=0)                    ! DATA TABLE FLAGS

C-    INITIALISE THE ROTATIONAL STATES TABLE

      DO INDEX=0,STATEMAX
         ORBSTATE(INDEX)=-1.0D0
         DIASTATE(INDEX)=-1.0D0
         ROTSTATE(INDEX)=-1.0D0
         END DO

C-    BUILD UP THE ROTATIONAL STATES TABLE

      INDEX=0
      J_MAX=INT(DSQRT(0.25+(E_LIMIT/ROT_J))-0.5)

      DO J_VAL=0,J_MAX

         WRITE (*,'(A,F6.2,A)') ' CREATING ROTATIONAL STATES TABLE: ',
     &    (100.0*DFLOAT(J_VAL+1)/DFLOAT(J_MAX+1)),'% DONE OF OUTER LOOP'

         J_ENE=ROT_J*DFLOAT(J_VAL*(J_VAL+1))
         I_MAX=INT(DSQRT(0.25+((E_LIMIT-J_ENE)/ROT_I))-0.5)

         DO I_VAL=0,I_MAX

            I_ENE=ROT_I*DFLOAT(I_VAL*(I_VAL+1))

            B_MIN=ABS(J_VAL-I_VAL)
            B_MAX=J_VAL+I_VAL

            K_MAX=MIN(INT(DSQRT((E_LIMIT-(J_ENE+I_ENE))/ROT_K)),J_VAL)

            DO K_VAL=0,K_MAX

               K_ENE=ROT_K*DFLOAT(K_VAL*K_VAL)
               R_ENE=I_ENE+J_ENE+K_ENE              ! ROTATIONAL ENERGY AT PRODUCTS
               DEGSTATE(INDEX)=0.0D0              ! SET DEGENERACY TO ZERO

               DO B_VAL=B_MIN,B_MAX

                  L_MIN=ABS(BIG_J-B_VAL)
                  L_MAX=BIG_J+B_VAL

                  DO L_VAL=L_MIN,L_MAX

                     DEGEN=0
                     DO M_VAL=-BIG_J,BIG_J
                        DO N_VAL=-J_VAL,J_VAL
                           TERM_1=MIN(B_VAL,L_VAL+M_VAL,I_VAL+N_VAL)
                           TERM_2=MIN(B_VAL,L_VAL-M_VAL,I_VAL-N_VAL)
                           DEGEN=DEGEN+MAX(TERM_1+TERM_2+1,0)
                           END DO
                        END DO
                     IF (K_VAL.GT.0) DEGEN=DEGEN*2

                     DEGSTATE(INDEX)=DEGSTATE(INDEX)+DEGEN

                     END DO ! L_VAL

                  END DO ! B_VAL

               IF (DEGSTATE(INDEX).GT.(0.0D0)) THEN
                  ROTSTATE(INDEX)=R_ENE              ! ROTATIONAL ENERGY
                  ORBSTATE(INDEX)=L_VAL
                  DIASTATE(INDEX)=MIN(I_VAL,1000)
                  INDEX=INDEX+1
                  IF (INDEX.GT.STATEMAX) THEN
                     PAUSE 'ROT STATES ARRAY NOT LARGE ENOUGH'
                     STOP
                     END IF
                  END IF

               END DO ! K_VAL

            END DO ! I_VAL

         END DO ! J_VAL

      RETURN
      END

