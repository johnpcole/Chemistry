      SUBROUTINE MAKE_VIB(VIBCONST,E_LIMIT,VIBSTATE,VSTATEMAX)

C-    THIS SUBROUTINE CREATES THE VIBRATIONAL DATA TABLE

      IMPLICIT NONE

      REAL*8 E1_LIM       ! ENERGY LIMIT ON TS VIBRATIONAL MODE 1
      REAL*8 E2_LIM       ! ENERGY LIMIT ON TS VIBRATIONAL MODE 2
      REAL*8 E3_LIM       ! ENERGY LIMIT ON TS VIBRATIONAL MODE 3
      REAL*8 E_LIMIT      ! AMOUNT OF AVAILABLE ENERGY
      INTEGER INDEX       ! VIBRATIONAL STATES TABLE INDEX
      INTEGER VSTATEMAX    ! VIBRATIONAL STATES TABLE SIZE
      REAL*8 V_ENE        ! TRANSITION STATE ENERGY
      REAL*8 V1_ENE       ! ENERGY OF TS VIBRATIONAL MODE 1
      REAL*8 V2_ENE       ! ENERGY OF TS VIBRATIONAL MODE 2
      REAL*8 V3_ENE       ! ENERGY OF TS VIBRATIONAL MODE 3
      INTEGER V1_MAX      ! QUANTUM NUMBER LIMIT ON VIBRATIONAL MODE 1
      INTEGER V2_MAX      ! QUANTUM NUMBER LIMIT ON VIBRATIONAL MODE 2
      INTEGER V3_MAX      ! QUANTUM NUMBER LIMIT ON VIBRATIONAL MODE 3
      INTEGER V1_VAL      ! QUANTUM NUMBER OF VIBRATIONAL MODE 1
      INTEGER V2_VAL      ! QUANTUM NUMBER OF VIBRATIONAL MODE 2
      INTEGER V3_VAL      ! QUANTUM NUMBER OF VIBRATIONAL MODE 3
      REAL*8 VIBCONST     ! VIBRATIONAL CONSTANTS DATA
      REAL*8 VIBSTATE     ! VIBRATIONAL STATES TABLE

      DIMENSION VIBCONST(1:3)           ! VIBRATIONAL CONSTANTS DATA
      DIMENSION VIBSTATE(0:VSTATEMAX)   ! VIBRATIONAL STATES TABLE

C-    INITIALISE THE VIBRATIONAL STATES TABLE

      DO INDEX=0,VSTATEMAX
         VIBSTATE(INDEX)=-1.0D0
         END DO

C-    BUILD UP THE VIBRATIONAL STATES TABLE

      INDEX=0
      E1_LIM=E_LIMIT
      V1_MAX=INT((E1_LIM/VIBCONST(1))-0.5)
      DO V1_VAL=0,V1_MAX
         V1_ENE=VIBCONST(1)*(DFLOAT(V1_VAL)+0.5)

      WRITE (*,'(A,F6.2,A)')     ' CREATING VIBRATIONAL STATES TABLE :',
     &                      (100.0D0*DFLOAT(1+V1_VAL)/DFLOAT(1+V1_MAX)),
     &                                  '% DONE OF OUTER LOOP'

         E2_LIM=E1_LIM-V1_ENE
         V2_MAX=INT((E2_LIM/VIBCONST(2))-0.5)
         DO V2_VAL=0,V2_MAX
            V2_ENE=VIBCONST(2)*(DFLOAT(V2_VAL)+0.5)

            E3_LIM=E2_LIM-V2_ENE
            V3_MAX=INT((E3_LIM/VIBCONST(3))-0.5)
            DO V3_VAL=0,V3_MAX
               V3_ENE=VIBCONST(3)*(DFLOAT(V3_VAL)+0.5)

               V_ENE=V1_ENE+V2_ENE+V3_ENE

               IF (V_ENE.LE.E_LIMIT) THEN
                  VIBSTATE(INDEX)=V_ENE

                     INDEX=INDEX+1
                     IF (INDEX.GT.VSTATEMAX) THEN
                        PAUSE 'VIB STATES ARRAY NOT LARGE ENOUGH'
                        STOP
                        END IF
                     END IF

               END DO ! V3_VAL
            END DO ! V2_VAL
         END DO ! V1_VAL

      RETURN
      END

