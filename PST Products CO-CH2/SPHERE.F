      FUNCTION SPHERE(L_VAL,ENERGY,MASS,GRID)

C- THIS FUNCTION CALCULATES THE TRANSLATIONAL NUMBER
C- OF STATES USING THE PARTICLE IN A SPHERE MODEL

      IMPLICIT NONE

      INTEGER INDEX,ENERGY,L_VAL
      INTEGER*4 GUESS,OLD_1,OLD_2,TRIES
      REAL*8 E,N,BESSEL,FACTOR,RESULT,MASS
      REAL*8 GRID,RADIUS,SPHERE,POLY

      PARAMETER (RADIUS=1.0D-5)               ! RADIUS OF SPHERE IN METRES

      DIMENSION E(2)
      DIMENSION N(2)

      FACTOR=6.62608D-34/3.14159265359D0      ! H / PI
      FACTOR=(FACTOR/RADIUS)**2.0D0           ! (H/(PI*RADIUS))**2
      FACTOR=FACTOR/(8.0D0*MASS)              ! (H/(PI*RADIUS))**2)/(8*MU)
      FACTOR=FACTOR*6.02214D23                ! CONVERT TO UNITS OF J/MOL

      N(1)=0.0D0
      N(2)=0.0D0

      IF (ENERGY.LT.0) GOTO 3000              ! FOR LEVELS ABOVE PHOTON ENERGY, THERE WILL BE NO TRANSLATIONAL ENERGY

      E(1)=DFLOAT(ENERGY)*GRID
      E(2)=E(1)+GRID      

      DO INDEX=1,2

C- THE INITIAL GUESS IS BASED ON THE CRUDE FORMULA E=((N+L/2)**2)*HH/8MRR

      POLY=8.0D0*MASS*E(INDEX)/6.02214D23
      POLY=DSQRT(POLY)*RADIUS/6.62608D-34
      POLY=POLY-(0.5D0*DFLOAT(L_VAL))
      IF (POLY.GT.(21.0D8)) THEN
         WRITE(6,*) 'TOO MANY TRANSLATIONAL LEVELS'
         WRITE(6,*) 'ERROR TERMINATION IN SPHERE SECTION'
         STOP
         END IF
      GUESS=MAX(INT(POLY),1)
      OLD_1=-999
      OLD_2=-999


C- FIND THE EXACT VALUE BY GENTLE MOVEMENT OF GUESS UNTIL
C- LAST TWO GUESSES ARE EITHER SIDE OF THE CORRECT VALUE

      DO TRIES=1,1000000

         RESULT=BESSEL(L_VAL,GUESS)
         RESULT=RESULT*RESULT*FACTOR

         OLD_2=OLD_1
         OLD_1=GUESS

         IF (RESULT.GT.E(INDEX)) THEN
            GUESS=GUESS-1
            IF (GUESS.LT.1) GOTO 1000
            ELSE
            GUESS=GUESS+1
            END IF

         IF (GUESS.EQ.OLD_2) GOTO 2000

         END DO

      WRITE(6,*) 'CANT FIND CORRECT LEVEL'
      WRITE(6,*) 'ERROR TERMINATION IN SPHERE SECTION'
      STOP

1000  CONTINUE

      GUESS=0
      OLD_1=0
      OLD_2=0

2000  CONTINUE

      N(INDEX)=DFLOAT(MIN(GUESS,OLD_1,OLD_2))

      END DO !INDEX

3000  CONTINUE

      SPHERE=N(2)-N(1)
      RETURN
      END

C-------------------------------------------------------------------------------------------
C===========================================================================================
C-------------------------------------------------------------------------------------------

      FUNCTION BESSEL(ORDER,N_VAL)

C- THIS FUNCTION CALCULATES THE Nth ZERO OF THE SPHERICAL BESSEL FUNCTION OF GIVEN ORDER
C- MCMAHON'S EXPRESSION TAKEN FROM HANDBOOK OF MATHEMATICAL FUNCTIONS, ABRAMOWITZ & SEGUN

      IMPLICIT NONE

      INTEGER ORDER,N_VAL
      REAL*8 V,S,B,M,TERM_1,TERM_2,TERM_3,TERM_4,BESSEL


      V=0.5D0+DFLOAT(ORDER)
      S=DFLOAT(N_VAL)
      B=8.0D0*3.14159265359D0*(S+(0.5D0*V)-0.25D0)  ! THIS IS THE EQUIVALENT TO 8*BETA IN THE BOOK
      M=4.0D0*V*V

      TERM_1=1/B

      TERM_2=(7.0D0*M)-31.0D0
      TERM_2=4.0D0*TERM_2/(3.0D0*B*B*B)

      TERM_3=(83.0D0*M*M)-(982.0D0*M)+3779.0D0
      TERM_3=32.0D0*TERM_3/(15.0D0*B*B*B*B*B)

      TERM_4=(6949D0*M*M*M)-(153855.0D0*M*M)+(1585743.0D0*M)-6277237.0D0
      TERM_4=64.0D0*TERM_4/(105.0D0*B*B*B*B*B*B*B)

      BESSEL=(B/8.0D0)-((M-1.0D0)*(TERM_1+TERM_2+TERM_3+TERM_4))
      RETURN
      END
