      SUBROUTINE GET_DATA(VIBCONST,E_LIMIT,ROT_J,ROT_K,BIG_J,BIG_K,
     &                                                      R_MASS)

      IMPLICIT NONE

      REAL*8 A,C,H        ! PHYSICAL CONSTANTS
      INTEGER BIG_J       ! TOTAL ANGULAR MOMENTUM
      INTEGER BIG_K       ! PRINCIPLE ANGULAR MOMENTUM
      REAL*8 E_LIMIT      ! AVAILABLE ENERGIES
      REAL*8 E_REAC       ! ENERGY OF REACTANT
      INTEGER INDEX       ! COUNTING VARIABLE
      REAL*8 PHOTON       ! ENERGY OF PHOTON
      REAL*8 ROT_A        ! ROTATIONAL CONSTANT OF FRAGMENT
      REAL*8 ROT_B        ! ROTATIONAL CONSTANT OF FRAGMENT
      REAL*8 ROT_C        ! ROTATIONAL CONSTANT OF FRAGMENT
      REAL*8 ROT_J        ! J ROTATIONAL CONSTANT OF FRAGMENT
      REAL*8 ROT_K        ! K ROTATIONAL CONSTANT OF FRAGMENT
      REAL*8 ROT_R        ! BIG_J ROTATIONAL CONSTANT OF REACTANT
      REAL*8 ROT_S        ! BIG_K ROTATIONAL CONSTANT OF REACTANT
      REAL*8 RROT_A       ! ROTATIONAL CONSTANT OF REACTANT
      REAL*8 RROT_B       ! ROTATIONAL CONSTANT OF REACTANT
      REAL*8 RROT_C       ! ROTATIONAL CONSTANT OF REACTANT
      REAL*8 VIBCONST     ! VIBRATIONAL CONSTANTS OF PRODUCTS AND TRANSITION STATE
      REAL*8 VIB_REAC     ! VIBRATIONAL CONSTANTS OF REACTANT
      REAL*8 ZERO_P_C     ! ZERO POINT CORRECTION FOR REACTANT
      CHARACTER*40 TEXT   ! TEMPORARY TEXT FLAG
	REAL*8 MASS_A,MASS_B,R_MASS

      PARAMETER (H=6.62608D-34)       ! PLANCK'S CONSTANT
      PARAMETER (C=2.99792458D8)      ! SPEED OF LIGHT
      PARAMETER (A=6.02214D23)        ! AVOGADRO'S NUMBER

      DIMENSION VIBCONST(1:6)         ! VIBRATIONAL CONSTANTS
      DIMENSION VIB_REAC(1:9)         ! VIBRATIONAL CONSTANTS OF REACTANT

C----------------------------------------------------------------------------
C===== DATA INPUT SECTION ===================================================
C----------------------------------------------------------------------------

      OPEN(77,FILE=('B:\DATA.INP'))
      READ(77,*) PHOTON

      READ(77,*) TEXT
      READ(77,*) E_REAC
      READ(77,*) RROT_c,RROT_B,RROT_A
      READ(77,*) (VIB_REAC(INDEX),INDEX=1,9)

      READ(77,*) TEXT
      READ(77,*) MASS_A,MASS_B
      READ(77,*) E_LIMIT
      READ(77,*) ROT_C,ROT_B,ROT_A
      READ(77,*) (VIBCONST(INDEX),INDEX=1,6)


      CLOSE(77)

C----------------------------------------------------------------------------
C===== DATA CONVERSION SECTION ==============================================
C----------------------------------------------------------------------------

      ! CONVERT ELECTRONIC ENERGIES INTO J/MOL

      E_REAC=E_REAC*2625.5*1000.0
      E_LIMIT=E_LIMIT*2625.5*1000.0

      ! CONVERT PHOTON ENERGY INTO J/MOL

      PHOTON=1.0D9*A*H*C/PHOTON

      ! CONVERT VIBRATIONAL CONSTANTS INTO J/MOL

      DO INDEX=1,6
         VIBCONST(INDEX)=100.0*A*H*C*VIBCONST(INDEX)
         END DO
      DO INDEX=1,9
         VIB_REAC(INDEX)=100.0*A*H*C*VIB_REAC(INDEX)
         END DO

      ! CONVERT ROTATIONAL CONSTANTS INTO J/MOL

      ROT_A=1.0D9*A*H*ROT_A
      ROT_B=1.0D9*A*H*ROT_B
      ROT_C=1.0D9*A*H*ROT_C
      RROT_A=1.0D9*A*H*RROT_A
      RROT_B=1.0D9*A*H*RROT_B
      RROT_C=1.0D9*A*H*RROT_C

      ! CONVERT ROTATIONAL CONSTANTS INTO J,K CONSTANTS

      ROT_J=DSQRT(ROT_A*ROT_B)
      ROT_K=ROT_C-ROT_J
      ROT_R=DSQRT(RROT_A*RROT_B)
      ROT_S=RROT_C-ROT_R

      ! CONVERT ELECTRONIC ENERGIES INTO AVAILABLE ENERGIES

      ZERO_P_C=0.0D0
      DO INDEX=1,9
         ZERO_P_C=ZERO_P_C+(0.5*VIB_REAC(INDEX))
         END DO

      ZERO_P_C=ZERO_P_C+(DFLOAT(BIG_J*(BIG_J+1))*ROT_R)
      ZERO_P_C=ZERO_P_C+(DFLOAT(BIG_K*BIG_K)*ROT_S)

      E_LIMIT=ZERO_P_C+PHOTON-(E_LIMIT-E_REAC)

      ! CONVERT MASSES INTO REDUCED MASS

      MASS_A=MASS_A/(A*1000.0)
      MASS_B=MASS_B/(A*1000.0)
      R_MASS=MASS_A*MASS_B/(MASS_A+MASS_B)

      RETURN
      END

