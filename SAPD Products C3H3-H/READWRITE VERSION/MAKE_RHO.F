      SUBROUTINE MAKE_RHO(ROTSTATE,STATEMAX,E_LIMIT,GRID,
     &       PROBMAX,PROB_KIN,PROB_VIB,PROB_ROT,GEOMAX,GEONUM,VIBCOUNT)

C- THIS SUBROUTINE CREATES THE KINETIC-NONKINETIC
C- NORMALISED PROBABILITY DISTRIBUTION ARRAY

      IMPLICIT NONE

      REAL*8 E_LIMIT      ! AMOUNT OF AVAILABLE ENERGY
      REAL*8 E_ROT        ! ROTATIONAL ENERGY OF FRAGMENT
      REAL*8 E_INT        ! INTERNAL ENERGY ON PATH
      REAL*8 E_STR        ! STRETCH ENERGY ON PATH
      REAL*8 GRID         ! INTEGRATION COARSENESS VALUE
      INTEGER INDEX       ! COUNTING FLAG
      INTEGER D,R         ! DATA TABLE FLAGS
      REAL*8 LEVELS       ! ROTATIONAL-KINETIC DEGENERACY
      REAL*8 NORMAL       ! NORMALISATION CONSTANT
      INTEGER P,T         ! DATA TABLE FLAGS
      REAL*8 P_KIN        ! PRODUCT KINETIC ENERGY
      REAL*8 P_NONVIB     ! PRODUCT NONVIB ENERGY
      REAL*8 P_TOTAL      ! PRODUCT TOTAL ENERGY
      REAL*8 P_VIB        ! PRODUCT VIBRATIONAL ENERGY
      REAL*8 PROB_KIN     ! KINETIC PROBABILITY ARRAY
      REAL*8 PROB_ROT     ! ROTATIONAL PROBABILITY ARRAY
      REAL*8 PROB_VIB     ! VIBRATIONAL PROBABILITY ARRAY
      INTEGER PROBMAX     ! PROBABILITY ARRAY SIZE
      INTEGER ROT_MARK    ! ROTATIONAL STATES TABLE INDEX
      INTEGER ROT_TOP     ! TOP OF ROTATIONAL STATES TABLE
      REAL*8 ROTSTATE     ! ROTATIONAL STATES TABLE
      INTEGER STATEMAX    ! DATA TABLES SIZE
      INTEGER VIB_MARK    ! VIBRATIONAL STATES TABLE INDEX
      REAL*8 VIBSTATE     ! VIBRATIONAL STATES TABLE
      REAL*8 T_KIN        ! TRANSITION STATE KINETIC ENERGY
      REAL*8 T_NONVIB     ! TRANSITION STATE NONVIB ENERGY
      REAL*8 T_ORB        ! TRANSITION STATE ORBITAL ENERGY
      REAL*8 T_STR        ! TRANSITION STATE STRETCH ENERGY
      REAL*8 T_TOTAL      ! TRANSITION STATE TOTAL ENERGY
      REAL*8 T_VIB        ! TRANSITION STATE VIBRATIONAL ENERGY
      LOGICAL ALLOW       ! FLAG FOR CHECKING ALONG PATH
      INTEGER PATH        ! MINIMUM ENERGY PATH POINTER
      INTEGER GEOMAX
      INTEGER GEONUM
      INTEGER ECHOMARK,ECHOTOP
      INTEGER VIBCOUNT

      PARAMETER (R=-1,D=-2)                   ! DATA TABLE FLAGS
      PARAMETER (P=-1,T=0)                    ! DATA TABLE FLAGS

      DIMENSION PROB_KIN(0:PROBMAX)           ! KINETIC PROBABILITY ARRAY
      DIMENSION PROB_ROT(0:PROBMAX)           ! ROTATIONAL PROBABILITY ARRAY
      DIMENSION PROB_VIB(0:PROBMAX)           ! VIBRATIONAL PROBABILITY ARRAY
      DIMENSION VIBSTATE(-1:GEOMAX)           ! VIBRATIONAL STATES TABLE
      DIMENSION ROTSTATE(-2:GEOMAX,0:STATEMAX)    ! ROTATIONAL STATES TABLE
      DIMENSION E_LIMIT(-1:GEOMAX)                ! AVAILABLE ENERGY

      OPEN (72,FILE=('VIBRATE.DAT'),STATUS=('OLD'))

C-    INITIALISE THE PROBABILITY ARRAY

      DO INDEX=0,PROBMAX
         PROB_KIN(INDEX)=0.0D0
         PROB_ROT(INDEX)=0.0D0
         PROB_VIB(INDEX)=0.0D0
         END DO

C-    FIND STATE TABLE LIMITS

      ROT_TOP=STATEMAX
      DO INDEX=STATEMAX,1,-1
         IF (ROTSTATE(T,INDEX).LT.(0.0D0)) ROT_TOP=INDEX-1
         END DO

C-    SET ENERGY LIMITS

      T_TOTAL=E_LIMIT(T)
      P_TOTAL=E_LIMIT(P)

      IF (INT(P_TOTAL/GRID).GT.PROBMAX) THEN
         PAUSE 'PROBAB ARRAY NOT BIG ENOUGH'
         STOP
         END IF

C-    BUILD UP PROBABILITY ARRAY

      DO ECHOMARK=0,VIBCOUNT,100

         WRITE (*,'(A,F6.2,A)')
     &                          ' CREATING PROBABILITY DISTRIBUTIONS: ',
     &                  (100.0D0*DFLOAT(ECHOMARK+1)/DFLOAT(VIBCOUNT+1)),
     &                                            '% DONE OF OUTER LOOP'

      ECHOTOP=MIN(VIBCOUNT,ECHOMARK+99)

      DO VIB_MARK=ECHOMARK,ECHOTOP

         READ(72,*) (VIBSTATE(INDEX),INDEX=-1,GEONUM)

         T_VIB=VIBSTATE(T)           ! VIBRATIONAL ENERGY AT T.S.
         P_VIB=VIBSTATE(P)           ! VIBRATIONAL ENERGY AT PRODUCT
         T_NONVIB=T_TOTAL-T_VIB               ! NON-VIBRATIONAL ENERGY AT T.S.
         P_NONVIB=P_TOTAL-P_VIB               ! NON-VIBRATIONAL ENERGY AT PRODUCT

         DO ROT_MARK=0,ROT_TOP

            E_ROT=ROTSTATE(R,ROT_MARK)        ! ROTATIONAL ENERGY (AT ALL POINTS)
            T_KIN=T_NONVIB-E_ROT              ! KINETIC ENERGY AT T.S.
            P_KIN=P_NONVIB-E_ROT              ! KINETIC ENERGY AT PRODUCT
            T_ORB=ROTSTATE(T,ROT_MARK)        ! ORBITAL ENERGY AT T.S.
            T_STR=T_KIN-T_ORB                 ! R.C. TRANSLATIONAL ENERGY AT T.S.

            IF (P_KIN.GE.(0.0D0)) THEN        ! LEVEL IS ALLOWED AT PRODUCT
               IF (T_STR.GE.(0.0D0)) THEN     ! LEVEL IS ALLOWED AT T.S.

                  ALLOW=.TRUE.

                  DO PATH=1,GEONUM
                     E_INT=E_ROT+VIBSTATE(PATH)+ROTSTATE(PATH,ROT_MARK)
                     E_STR=E_LIMIT(PATH)-E_INT
                     IF (E_STR.LT.(0.0D0)) ALLOW=.FALSE.
                     END DO

                  IF (ALLOW) THEN
                     LEVELS=ROTSTATE(D,ROT_MARK)
                     PROB_KIN(INT(P_KIN/GRID))=PROB_KIN(INT(P_KIN/GRID))
     &                                                           +LEVELS
                     PROB_VIB(INT(P_VIB/GRID))=PROB_VIB(INT(P_VIB/GRID))
     &                                                           +LEVELS
                     PROB_ROT(INT(E_ROT/GRID))=PROB_ROT(INT(E_ROT/GRID))
     &                                                           +LEVELS
                     END IF
                  END IF
               END IF

            END DO ! ROT

         END DO ! VIB         

         END DO !ECHO

      CLOSE(72)

C-    NORMALISE PROBABILITY ARRAY

      NORMAL=0.0D0
      DO INDEX=0,PROBMAX
         NORMAL=NORMAL+PROB_KIN(INDEX)
         NORMAL=NORMAL+PROB_ROT(INDEX)
         NORMAL=NORMAL+PROB_VIB(INDEX)
         END DO
      NORMAL=NORMAL/3.0D0
      DO INDEX=0,PROBMAX
         PROB_KIN(INDEX)=PROB_KIN(INDEX)/NORMAL
         PROB_ROT(INDEX)=PROB_ROT(INDEX)/NORMAL
         PROB_VIB(INDEX)=PROB_VIB(INDEX)/NORMAL
         END DO

      RETURN
      END

